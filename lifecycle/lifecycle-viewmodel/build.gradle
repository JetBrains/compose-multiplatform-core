/*
 * Copyright 2017 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * This file was created using the `create_project.py` script located in the
 * `<AndroidX root>/development/project-creator` directory.
 *
 * Please use that script when creating a new project, rather than copying an existing project and
 * modifying its settings.
 */

import androidx.build.KmpPlatformsKt
import androidx.build.PlatformIdentifier
import androidx.build.Publish
import org.jetbrains.kotlin.gradle.dsl.ExplicitApiMode
import org.jetbrains.kotlin.gradle.plugin.KotlinPlatformType

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
}

def macEnabled = KmpPlatformsKt.enableMac(project)
def linuxEnabled = KmpPlatformsKt.enableLinux(project)
def jsEnabled = KmpPlatformsKt.enableJs(project)
def wasmEnabled = KmpPlatformsKt.enableWasm(project)

androidXMultiplatform {
    android()
    desktop()
    mac()
    linux()
    ios()

    defaultPlatform(PlatformIdentifier.ANDROID)
}

kotlin {
    js {
        browser()
    }
    wasmJs()

    explicitApi = ExplicitApiMode.Strict

    sourceSets {
        commonMain {
            dependencies {
                api(project(":annotation:annotation"))
                api(libs.kotlinStdlib)
                api(libs.kotlinCoroutinesCore)
                implementation(libs.atomicFu)
            }
        }

        commonTest {
            dependencies {
                // TODO(b/214568825): migrate from `androidUnitTest` to here.
            }
        }

        jvmMain {
            dependsOn(commonMain)
        }

        androidMain {
            dependsOn(jvmMain)
            dependencies {
                api(libs.kotlinCoroutinesAndroid)
            }
        }

        androidUnitTest {
            dependsOn(commonTest)
            dependencies {
                implementation(libs.junit)
                implementation(libs.mockitoCore4)
                implementation(libs.kotlinTest)
                implementation(libs.truth)
            }
        }

        androidInstrumentedTest {
            dependsOn(commonTest)
            dependencies {
                implementation("androidx.core:core-ktx:1.2.0")
                implementation(libs.truth)
                implementation(libs.kotlinStdlib)
                implementation(libs.junit)
                implementation(libs.testExtJunit)
                implementation(libs.testCore)
                implementation(libs.testRunner)
            }
        }

        desktopMain {
            dependsOn(jvmMain)
        }

        if (macEnabled || linuxEnabled || jsEnabled || wasmEnabled) {
            nonJvmMain.dependsOn(commonMain)
        }
        if (macEnabled || linuxEnabled) {
            nativeMain.dependsOn(nonJvmMain)
        }
        if (jsEnabled || wasmEnabled) {
            webMain.dependsOn(nonJvmMain)
        }

        targets.all { target ->
            if (target.platformType == KotlinPlatformType.native) {
                target.compilations["main"].defaultSourceSet {
                    dependsOn(nativeMain)
                }
            } else if (target.platformType in [
                    KotlinPlatformType.js,
                    KotlinPlatformType.wasm
            ]) {
                target.compilations["main"].defaultSourceSet {
                    dependsOn(webMain)
                }
            }
        }
    }
}

android {
    buildTypes.all {
        consumerProguardFiles "proguard-rules.pro"
    }
    namespace "androidx.lifecycle.viewmodel"
}

androidx {
    name = "Lifecycle ViewModel"
    publish = Publish.SNAPSHOT_AND_RELEASE
    inceptionYear = "2017"
    description = "Android Lifecycle ViewModel"
    metalavaK2UastEnabled = true
}
